# PRQL functions for tv
# Prepended to all PRQL queries

# Frequency: group by cols, count, sort desc
let freq = func c tbl <relation> -> (
  from tbl
  group {c} (aggregate {Cnt = count this})
  sort {-Cnt}
)

# Distinct values of column (for hints)
let uniq = func c tbl <relation> -> (
  from tbl
  group {c} (take 1)
  select {c}
)

# Row count
let cnt = func tbl <relation> -> (
  from tbl
  aggregate {n = count this}
)

# Numeric column stats (n, min, max, avg, std)
let stats = func c tbl <relation> -> (
  from tbl
  aggregate {
    n = count this,
    min = min c,
    max = max c,
    avg = average c,
    std = stddev c
  }
)

# String column stats (count + distinct)
let cntdist = func c tbl <relation> -> (
  from tbl
  aggregate {
    n = count this,
    dist = count_distinct c
  }
)

# Column meta: cnt non-null, distinct, total, min, max
let meta = func c tbl <relation> -> (
  from tbl
  aggregate {
    cnt = s"COUNT({c})",
    dist = count_distinct c,
    total = count this,
    min = min c,
    max = max c
  }
)
